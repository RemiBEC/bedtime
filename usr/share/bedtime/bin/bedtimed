#!/usr/bin/perl

BEGIN {push @INC,'/usr/share/bedtime/bin'}

use strict;
use warnings;
use POSIX qw( setsid );
use IO::Socket;
use BedtimeDB qw( trim );

open (CONF,'/etc/bedtime.conf') or die "Cannot open configuration file - $!\n";
# Read the conf file into an array and filter out all lines consisting of just white space and comments
my @conf = <CONF>; close CONF;
@conf = grep (!/^\s*$/,@conf);
@conf = grep (!/^#/,@conf);

# Read the remaining lines into a hash split on =
my %vals;
foreach (@conf) {
   chomp;
   my @pair = split(/\s*=\s*/);
   $vals{trim($pair[0])}=trim($pair[1]);
}
# Get the TCP port number
my $btport = $vals{'btport'};

# Open the socket on the TCP port from the config
my $socket = IO::Socket::INET->new(
   'Proto'     => 'tcp',
   'LocalPort' => $btport,
   'Listen'    => 5,
   'Reuse'     => 1,
);
die "Cannot open socket on port $btport: $!" unless ($socket);

# Disconnect from the terminal
chdir '/' or die "Cannot cd to /: $!";
open STDIN, '/dev/null' or die "Cannot read /dev/null: $!";
open STDOUT, '>/dev/null' or die "Cannot write /dev/null: $!";
defined(my $pid = fork) or die "Cannot fork: $!";
exit if $pid;
setsid or die "Cannot start new session: $!";
open STDERR, '>&STDOUT' or exec("logger Cannot duplicate output: $!");

# Write the process ID
open  PID, ">/var/run/bedtimed.pid" or exec("logger Cannot open pid file: $!");
print PID  "$$\n" or exec("logger Cannot write to pid file: $!");
close PID;

# Launch scripts according to socket input
while ( my $client = $socket->accept() ) {
   chomp ( my $input = <$client> );
   system("logger socket $btport received $input");
   system("/usr/share/bedtime/bin/update_ipt") if $input =~ /^u/;
   system("/usr/share/bedtime/bin/dhcp_slurp") if $input =~ /^d/;
   system("/usr/share/bedtime/bin/get_manlst") if $input =~ /^m/;
   system("/usr/share/bedtime/bin/survey_net") if $input =~ /^s/;
   if ($input =~ /^s/) {
      $input =~ s/^s//;
      system("/usr/share/bedtime/bin/survey_net $input")
   }
   if ($input =~ /^p/) {
      $input =~ s/^p//;
      system("/usr/share/bedtime/bin/btsetup $input");
   }
   close $client;
}
