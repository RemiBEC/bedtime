#!/usr/bin/perl

use strict;
use warnings;
use POSIX qw( setsid );
use IO::Socket;

# Open the socket on TCP 5000
my $socket = IO::Socket::INET->new(
   'Proto'     => 'tcp',
   'LocalPort' => 5000,
   'Listen'    => 5,
   'Reuse'     => 1,
);
die "Cannot open socket on port 5000: $!" unless ($socket);

# Disconnect from the terminal
chdir '/' or die "Cannot cd to /: $!";
open STDIN, '/dev/null' or die "Cannot read /dev/null: $!";
open STDOUT, '>/dev/null' or die "Cannot write /dev/null: $!";
defined(my $pid = fork) or die "Cannot fork: $!";
exit if $pid;
setsid or die "Cannot start new session: $!";
open STDERR, '>&STDOUT' or exec("logger Cannot duplicate output: $!");

# Write the process ID
open  PID, ">/var/run/bedtimed.pid" or exec("logger Cannot open pid file: $!");
print PID  "$$\n" or exec("logger Cannot write to pid file: $!");
close PID;

# Launch scripts according to socket input
while ( my $client = $socket->accept() ) {
   chomp ( my $input = <$client> );
   system("update_ipt") if $input =~ /^u/;
   system("dhcp_slurp") if $input =~ /^d/;
   system("get_manlst") if $input =~ /^m/;
   close $client;
}
